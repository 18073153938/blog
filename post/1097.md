Title: 获取任意位置与圆直径形成的等腰三角形的底边与圆的交点的坐标
Date: 2010-08-18 11:43:30
Author: zrong
Postid: 1097
Slug: 1097
Nicename: %e8%8e%b7%e5%8f%96%e4%bb%bb%e6%84%8f%e4%bd%8d%e7%bd%ae%e4%b8%8e%e5%9c%86%e7%9b%b4%e5%be%84%e5%bd%a2%e6%88%90%e7%9a%84%e7%ad%89%e8%85%b0%e4%b8%89%e8%a7%92%e5%bd%a2%e7%9a%84%e5%ba%95%e8%be%b9%e4%b8%8e
Category: technology
Tags: as3, math
Attachments: 1098, 1099

### 原理图  
[![判断鼠标位置与圆直径形成的等腰三角形的底边与圆的交点的坐标原理图](/wp-content/uploads/2010/08/circle_dia.png "circle_dia")](/wp-content/uploads/2010/08/circle_dia.png)

<!--more-->

### 效果

[kml\_flashembed publishmethod="dynamic" fversion="10.0.0"
useexpressinstall="true" replaceId="simpleSample"
movie="/wp-content/uploads/2010/08/SimpleSample5.swf" width="400"
height="400" targetclass="flashmovie"]  
[![Get Adobe Flash
player](http://www.adobe.com/images/shared/download_buttons/get_flash_player.gif)](http://adobe.com/go/getflashplayer)  
[/kml\_flashembed]

### 源码

``` {lang="actionscript" line="1" file="SimpleSample5.as"}
package
{
    import flash.display.Sprite;
    import flash.events.MouseEvent;
    
    [SWF(width=400,height=400)]
    public class SimpleSample5 extends Sprite
    {
        private var _centerX:Number;
        private var _centerY:Number;
        private var _radius:Number = 80;
        
        public function SimpleSample5()
        {
            _centerX = stage.stageWidth/2;
            _centerY = stage.stageHeight/2;
            draw();
            drawTriangle(0, 0);
            stage.addEventListener(MouseEvent.MOUSE_MOVE, mouseMoveHandler);
        }
        
        private function draw():void
        {
            graphics.clear();
            graphics.lineStyle(1);
            graphics.moveTo(0, stage.stageHeight/2);
            graphics.lineTo(stage.stageWidth, _centerY);
            graphics.moveTo(_centerX, 0);
            graphics.lineTo(_centerX, stage.stageHeight);
            graphics.drawCircle(_centerX, _centerY, _radius);
        }
        
        private function drawTriangle($mouseX:Number, $mouseY:Number):void
        {
            var __dx:Number = _centerX - $mouseX;
            var __dy:Number = _centerY - $mouseY;
            var __distance:Number = Math.sqrt(__dx*__dx + __dy*__dy);
            //计算点击处与圆心夹角的角度并在坐标系中旋转90度
            var __radian:Number = Math.atan2(__dy, __dx) - Math.PI/2;
            var __x2:Number = _radius * Math.cos(__radian);
            var __y2:Number = _radius * Math.sin(__radian);
            graphics.moveTo(_centerX, _centerY);
            graphics.lineTo($mouseX, $mouseY);
            graphics.lineTo(_centerX+__x2, _centerY+__y2);
            graphics.lineTo(_centerX, _centerY);
            graphics.lineTo(_centerX-__x2, _centerY-__y2);
            graphics.lineTo($mouseX, $mouseY);
        }
        
        private function mouseMoveHandler(evt:MouseEvent):void
        {
            draw();
            drawTriangle(mouseX, mouseY);
        }
    }
}
```
